import "../components/Button.coi";

component TodoItem(pub int id, pub string text, pub mut bool done, def onRemove(int) : void) {
    def toggle() : void {
        done = !done;
    }

    style {
        .item {
            display: flex;
            align-items: center;
            gap: 12px;
            padding: 12px 16px;
            background: #fff;
            border: 1px solid #eee;
            border-radius: 8px;
            transition: all 0.2s ease;
        }
        .item:hover {
            border-color: #ddd;
        }
        .checkbox {
            width: 20px;
            height: 20px;
            border: 2px solid #ddd;
            border-radius: 4px;
            cursor: pointer;
            display: flex;
            align-items: center;
            justify-content: center;
            transition: all 0.2s ease;
        }
        .checkbox:hover {
            border-color: #9477ff;
        }
        .checkbox.checked {
            background: #9477ff;
            border-color: #9477ff;
        }
        .checkmark {
            color: #fff;
            font-size: 14px;
        }
        .text {
            flex: 1;
            font-size: 14px;
            color: #333;
        }
        .text.done {
            text-decoration: line-through;
            color: #999;
        }
        .remove {
            width: 24px;
            height: 24px;
            border: none;
            background: transparent;
            color: #ccc;
            cursor: pointer;
            font-size: 18px;
            line-height: 24px;
            text-align: center;
            padding: 0;
            border-radius: 4px;
            transition: all 0.2s ease;
        }
        .remove:hover {
            background: #fee;
            color: #e55;
        }
    }

    view {
        <div class="item">
            <if done>
                <button class="remove" onclick={onRemove(id)}>×</button>
                <div class="checkbox checked" onclick={toggle}>
                    <span class="checkmark">✓</span>
                </div>
                <span class="text done">{text}</span>
            <else>
                <div class="checkbox" onclick={toggle}></div>
                <span class="text">{text}</span>
                <button class="remove" onclick={onRemove(id)}>×</button>
            </else>
            </if>
        </div>
    }
}

component TodoList {
    mut TodoItem[] todos;
    mut int nextId = 0;

    init {
        todos.push(TodoItem(nextId++, "Learn Coi", true));
        todos.push(TodoItem(nextId++, "Build something cool", false));
        todos.push(TodoItem(nextId++, "Share with friends", false));
    }

    def addTodo() : void {
        todos.push(TodoItem(nextId++, "New task", false));
    }

    def removeTodo(int id) : void {
        mut TodoItem[] newTodos;
        for todo in todos {
            if (todo.id != id) {
                newTodos.push(todo);
            }
        }
        todos = newTodos;
    }

    style {
        .todo-list {
            display: flex;
            flex-direction: column;
            gap: 16px;
            width: 320px;
        }
        .header {
            display: flex;
            justify-content: space-between;
            align-items: center;
        }
        .label {
            font-size: 12px;
            text-transform: uppercase;
            letter-spacing: 1px;
            color: #999;
        }
        .items {
            display: flex;
            flex-direction: column;
            gap: 8px;
            min-height: 50px;
        }
    }

    view {
        <div class="todo-list">
            <div class="header">
                <div class="label">Todo List</div>
                <Button label="+ Add" type="secondary" &onclick={addTodo} />
            </div>
            <div class="items">
                <for todo in todos key={todo.id}>
                    <{todo}
                        &onRemove={removeTodo(todo.id)}
                    />
                </for>
            </div>
        </div>
    }
}
