import "../demos/Counter.coi";
import "../demos/AnimatedBox.coi";
import "../demos/CanvasDemo.coi";
import "../demos/TodoList.coi";
import "../demos/InputDemo.coi";
import "../demos/AudioPlayer.coi";
import "../components/CodeView.coi";

component DemoSection {
    string counterCode = `component Counter {
    mut int count = 0;

    def increment() : void {
        count++;
    }

    def decrement() : void {
        count--;
    }

    view {
        <div class="counter">
            <div class="count-text">{count}</div>
            <div class="buttons">
                <Button label="-" &onclick={decrement} />
                <Button label="+" &onclick={increment} />
            </div>
        </div>
    }
}`;

    string animatedBoxCode = `component AnimatedBox {
    mut float x = 0;
    mut int direction = 1;

    tick(float dt) {
        x += direction * dt * 200;
    
        if (x > 300) direction = -1;
        if (x < 0) direction = 1;
    }

    view {
        <div class="container">
            <div class="box" style="left: \${x}px;"></div>
        </div>
    }
}`;

    string canvasCode = `component CanvasDemo {
    mut Canvas canvas;
    mut CanvasContext2D ctx;
    mut float ballX = 150;
    mut float ballY = 50;
    mut float velY = 0;
    float gravity = 1500;

    mount {
        canvas.setSize(300, 200);
        ctx = canvas.getContext2d();
    }

    tick(float dt) {
        velY += gravity * dt;
        ballY += velY * dt;
        
        if (ballY > 185) {
            ballY = 185;
            velY = -velY * 0.7;
        }

        ctx.clearRect(0, 0, 300, 200);
        ctx.arc(ballX, ballY, 15, 0, 6.28);
        ctx.fill();
    }

    view {
        <canvas &={canvas}></canvas>
    }
}`;

    string inputCode = `component InputDemo {
    mut string text = "";
    mut int charCount = 0;

    def handleInput(string s) : void {
        text = s;
        charCount = text.length();
    }

    view {
        <div class="input-demo">
            <input 
                placeholder="Type something..." 
                oninput={handleInput}
            />
            <div class="preview">{text}</div>
            <div class="stats">
                Characters: {charCount}
            </div>
        </div>
    }
}`;

    string todoCode = `component TodoItem(
    pub int id,
    pub string text,
    pub mut bool done,
    def onRemove(int) : void
) {
    view {
        <div class="item">
            <span>{text}</span>
            <button onclick={onRemove(id)}>×</button>
        </div>
    }
}

component TodoList {
    mut TodoItem[] todos;
    mut int nextId = 0;

    init {
        todos.push(TodoItem(nextId++, "Learn Coi", true));
        todos.push(TodoItem(nextId++, "Build something cool", false));
    }

    def addTodo() : void {
        todos.push(TodoItem(nextId++, "New task", false));
    }

    def removeTodo(int id) : void {
        mut TodoItem[] newTodos;
        for todo in todos {
            if (todo.id != id) {
                newTodos.push(todo);
            }
        }
        todos = newTodos;
    }

    view {
        <for todo in todos key={todo.id}>
            <{todo}
                &onRemove={removeTodo}
            />
        </for>
    }
}`;

    string audioPlayerCode = `component AudioPlayer {
    mut Audio audio;
    mut bool playing = false;
    mut float currentTime = 0;
    mut float duration = 0;
    mut float progress = 0;

    mount {
        audio = Audio.createAudio("song.mp3");
    }

    tick(float dt) {
        if (playing) {
            currentTime = audio.getCurrentTime();
            duration = audio.getDuration();
            if (duration > 0) {
                progress = (currentTime / duration) * 100;
            }
        }
    }

    def togglePlay() : void {
        if (playing) audio.pause();
        else audio.play();
        playing = !playing;
    }

    view {
        <div class="progress" style="width:\${progress}%"></div>
        <div class="time">
            <span>{currentTime}s</span>
            <span>{duration}s</span>
        </div>
        <button onclick={togglePlay}>
            <if playing><span>⏸</span>
            <else><span>▶</span></else>
            </if>
        </button>
    }
  }`;

    style {
        .demo-section {
            padding: 80px 20px;
            display: flex;
            flex-direction: column;
            align-items: center;
            gap: 60px;
            border-top: 1px solid #eee;
            background: #ffffff;
        }
        h2 {
            font-size: 32px;
            color: #1a1a1a;
            margin: 0;
            font-weight: 600;
        }
        .demo-row {
            display: flex;
            gap: 32px;
            align-items: center;
            justify-content: center;
            flex-wrap: wrap;
            width: 100%;
        }
        .demo-wrapper {
            width: 100%;
            max-width: 380px;
            display: flex;
            justify-content: center;
        }

        @media (max-width: 768px) {
            .demo-section {
                padding: 40px 16px;
                gap: 40px;
            }
            .demo-row {
                gap: 24px;
            }
        }
    }

    view {
        <div class="demo-section">
            <h2>Demos</h2>
            <div class="demo-row">
                <div class="demo-wrapper">
                    <TodoList />
                </div>
                <CodeView code={todoCode} width="600px" />
            </div>
                <div class="demo-row">
                <div class="demo-wrapper">
                    <InputDemo />
                </div>
                <CodeView code={inputCode} width="600px" />
            </div>
            <div class="demo-row">
                <div class="demo-wrapper">
                    <Counter />
                </div>
                <CodeView code={counterCode} width="600px" />
            </div>
            <div class="demo-row">
                <div class="demo-wrapper">
                    <CanvasDemo />
                </div>
                <CodeView code={canvasCode} width="600px" />
            </div>
            <div class="demo-row">
                <div class="demo-wrapper">
                    <AnimatedBox />
                </div>
                <CodeView code={animatedBoxCode} width="600px" />
            </div>
            <div class="demo-row">
                <div class="demo-wrapper">
                    <AudioPlayer />
                </div>
                <CodeView code={audioPlayerCode} width="600px" />
            </div>
         
        </div>
    }
}
